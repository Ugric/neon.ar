import "__parse_value" as *
import "__parse_comment" as *

let startingsIndex = {'{': 0, '%': 1, '#': 2}
let startings = ["{", "%", "#"]
let endings = ["}", "%", "#"]

let __parser(html) = do
    let positions = []

    let output = []
    let insideTemplate = false
    let templateType = null
    let lastPos = 0
    let i = 0
    while (i < html.length) do
        if (insideTemplate) do
            let splitted  = html[i:].split(endings[templateType]+"}")
            if (splitted.length == 0) throwError("parsing error in neon.ar", "invalid syntax")
            let looking_at = 0
            let parsedText = null
            let parsed = {hasParsed:false}
            while ((not parsed.hasParsed) && looking_at < splitted.length-1) do
                looking_at+=1
                parsedText = splitted[:looking_at].join(endings[templateType]+"}")
                if (templateType == 0) parsed = __parse_value(parsedText)
                else if (templateType == 2) parsed = __parse_comment(parsedText)
            if (not parsed) throwError("parsing error in neon.ar", "invalid syntax")
            if (parsed.value) output.append(parsed.value)
            i+=parsedText.length+2
            insideTemplate = false
            lastPos = i
        else do
            if (html[i] != "{" || i+1>=html.length || html[i+1] not in startings) do
                i+=1
                continue
            insideTemplate = true
            templateType = startingsIndex[html[i+1]]
            output.append({type:"html", html:html[lastPos:i]})
            i+=2
    output.append({type:"html", html:html[lastPos:]})
    return output